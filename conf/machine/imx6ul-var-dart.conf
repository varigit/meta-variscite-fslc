#@TYPE: Machine
#@NAME: Variscite DART-6UL
#@SOC: i.MX6UL
#@DESCRIPTION: Machine configuration for Variscite DART-6UL
#@MAINTAINER: Eran Matityahu <eran.m@variscite.com>
#
# http://www.variscite.com

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa7.inc

SOC_FAMILY = "mx6ul"
DEFAULTTUNE_mx6ul ?= "cortexa7hf-neon"

SDCARD_GENERATION_COMMAND_mx6ul = "generate_imx_sdcard"

SERIAL_CONSOLE = "115200 ttymxc0"
MACHINE_FEATURES += " pci bluetooth touchscreen wifi"

# UBIFS for Variscite NAND 0.5 GB
MKUBIFS_ARGS = " -m 2048 -e 124KiB -c 3965 "
UBINIZE_ARGS = " -m 2048 -p 128KiB -s 2048 -O 2048 "

# UBIFS for Variscite NAND 1.0 GB (models with erase block size of 128KiB)
#MKUBIFS_ARGS = " -m 2048 -e 124KiB -c 7855 "
#UBINIZE_ARGS = " -m 2048 -p 128KiB -s 2048 -O 2048 "

# UBIFS for Variscite NAND 1.0 GB (models with erase block size of 512KiB)
#MKUBIFS_ARGS = " -m 4096 -e 504KiB -c 2000 "
#UBINIZE_ARGS = " -m 4096 -p 512KiB -s 4096 -O 4096 "

UBI_VOLNAME  = "rootfs"

MACHINE_SOCARCH_FILTER_mx6ul = "libfslcodec libfslparser gst-fsl-plugins"

PREFERRED_VERSION_imx-kobs = "5.1"

# Variscite BSP default providers
PREFERRED_PROVIDER_virtual/kernel_imx6ul-var-dart ?= "linux-variscite"
PREFERRED_VERSION_linux-variscite = "4.1.15"
KERNEL_DEVICETREE = " \
		     imx6ul-var-dart-emmc_wifi.dtb \
		     imx6ul-var-dart-sd_emmc.dtb \
		     imx6ul-var-dart-nand_wifi.dtb \
		     imx6ul-var-dart-sd_nand.dtb \
		     "
KERNEL_IMAGETYPE = "zImage"

PREFERRED_PROVIDER_u-boot_imx6ul-var-dart = "u-boot-variscite"
PREFERRED_PROVIDER_virtual/bootloader_imx6ul-var-dart = "u-boot-variscite"

UBOOT_MAKE_TARGET = ""
SPL_BINARY = "SPL"
UBOOT_SUFFIX = "img"
UBOOT_CONFIG ??= "nand sd "
# The above order matters. The last one will be also called u-boot-${MACHINE} and will be used to create the sdcard.
UBOOT_CONFIG[sd] = "mx6ul_var_dart_mmc_defconfig,sdcard"
UBOOT_CONFIG[nand] = "mx6ul_var_dart_nand_defconfig,ubifs"

PREFERRED_PROVIDER_wpa-supplicant-cli = "wpa-supplicant"
PREFERRED_PROVIDER_wpa-supplicant-passphrase = "wpa-supplicant"
PREFERRED_PROVIDER_wpa-supplicant = "wpa-supplicant"

MACHINE_EXTRA_RDEPENDS += " \
			   imx-kobs \
			   bcm4343w-fw \
			   brcm-patchram-plus \
			   kernel-modules \
			   tslib-calibrate \
			   tslib-tests \
			   hostapd \
			   u-boot-fw-utils \
			   ${@bb.utils.contains('DISTRO_FEATURES', 'x11', "x11vnc", "", d)} \
			   "

BBMASK_prepend = "fsl-image-mfgtool-initramfs "

# modules for bluez snd supporting
MACHINE_EXTRA_RDEPENDS += " \
	pulseaudio \
	pulseaudio-server \
	pulseaudio-misc \
	pulseaudio-module-dbus-protocol \
	pulseaudio-module-cli \
	pulseaudio-module-device-manager \
	pulseaudio-lib-bluez5-util \
	pulseaudio-module-bluez5-discover \
	pulseaudio-module-bluez5-device \
	pulseaudio-module-bluetooth-policy \
	pulseaudio-module-bluetooth-discover \
"

IMAGE_FSTYPES_imx6ul-var-dart ?= "tar.bz2 ext4 sdcard ubi"
