#@TYPE: Machine
#@NAME: Variscite VAR-SOM-MX7
#@SOC: i.MX7D
#@DESCRIPTION: Machine configuration for Variscite VAR-SOM-MX7
#@MAINTAINER: Eran Matityahu <eran.m@variscite.com>
#
# http://www.variscite.com

MACHINEOVERRIDES =. "mx7:mx7d:"
require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa7.inc

SOC_FAMILY = "mx7:mx7d"
DEFAULTTUNE_mx7 ?= "cortexa7hf-neon"

SDCARD_GENERATION_COMMAND_mx7 = "generate_imx_sdcard"

SERIAL_CONSOLE = "115200 ttymxc0"
MACHINE_FEATURES += " pci bluetooth touchscreen wifi"

# UBIFS for Variscite NAND 0.5 GB
MKUBIFS_ARGS = " -m 2048 -e 124KiB -c 3965 "
UBINIZE_ARGS = " -m 2048 -p 128KiB -s 2048 -O 2048 "

# UBIFS for Variscite NAND 1.0 GB (models with erase block size of 128KiB)
#MKUBIFS_ARGS = " -m 2048 -e 124KiB -c 7855 "
#UBINIZE_ARGS = " -m 2048 -p 128KiB -s 2048 -O 2048 "

# UBIFS for Variscite NAND 1.0 GB (models with erase block size of 512KiB)
#MKUBIFS_ARGS = " -m 4096 -e 504KiB -c 2000 "
#UBINIZE_ARGS = " -m 4096 -p 512KiB -s 4096 -O 4096 "

UBI_VOLNAME  = "rootfs"

# mx7 GPU libraries
PREFERRED_PROVIDER_virtual/egl_mx7 = "mesa"
PREFERRED_PROVIDER_virtual/libgles1_mx7 = "mesa"
PREFERRED_PROVIDER_virtual/libgles2_mx7 = "mesa"
PREFERRED_PROVIDER_virtual/libgl_mx7 = "mesa"
PREFERRED_PROVIDER_virtual/libg2d_mx7 = ""

MACHINE_SOCARCH_FILTER_mx7 = "libfslcodec libfslparser gst-fsl-plugins"

# Variscite BSP default providers
PREFERRED_PROVIDER_virtual/kernel_imx7-var-som ?= "linux-variscite"
PREFERRED_VERSION_linux-variscite = "4.1.15"
KERNEL_DEVICETREE = " \
		     imx7d-var-som-emmc.dtb \
		     imx7d-var-som-nand.dtb \
		     imx7d-var-som-emmc-m4.dtb \
		     imx7d-var-som-nand-m4.dtb \
		     "
KERNEL_IMAGETYPE = "zImage"

PREFERRED_PROVIDER_u-boot_imx7-var-som = "u-boot-variscite"
PREFERRED_PROVIDER_virtual/bootloader_imx7-var-som = "u-boot-variscite"

UBOOT_MAKE_TARGET = ""
SPL_BINARY = "SPL"
UBOOT_SUFFIX = "img"
UBOOT_CONFIG ??= "nand sd "
# The above order matters. The last one will be also called u-boot-${MACHINE} and will be used to create the sdcard.
UBOOT_CONFIG[sd] = "mx7dvar_som_defconfig,sdcard"
UBOOT_CONFIG[nand] = "mx7dvar_som_nand_defconfig,ubifs"

PREFERRED_PROVIDER_wpa-supplicant-cli = "wpa-supplicant"
PREFERRED_PROVIDER_wpa-supplicant-passphrase = "wpa-supplicant"
PREFERRED_PROVIDER_wpa-supplicant = "wpa-supplicant"

MACHINE_EXTRA_RDEPENDS += " \
			   imx-kobs \
			   bcm4343w-fw \
			   bcm43xx-utils \
			   brcm-patchram-plus \
			   kernel-modules \
			   tslib-calibrate \
			   tslib-tests \
			   bluez5 \
			   ${@bb.utils.contains('DISTRO_FEATURES', 'pulseaudio', "variscite-bluetooth-snd-dev", "", d)} \
			   hostapd \
			   wpa-supplicant \
			   iw \
			   u-boot-fw-utils \
			   u-boot-splash \
			   ${@bb.utils.contains('DISTRO_FEATURES', 'x11', "x11vnc", "", d)} \
			   bridge-utils \
			   connman \
			   pm-utils \
			   "
# Packages added to all images (including core-image-minimal)
IMAGE_INSTALL_append = " \
        fsl-rc-local \
"

BBMASK_prepend = "fsl-image-mfgtool-initramfs "

IMAGE_FSTYPES = "tar.bz2 ext4 sdcard ubi"
